# Variables
BINDINGS_DIR := "./bindings-rs/src/contract"
DEPLOYMENTS_DIR := "./deployments"
DEVNET_STATE := "state.json"
SRC_DIR := "."
BINDINGS_FILTER := "DaveConsensus|DaveAppFactory"

default: build

build: build-smart-contracts bind
clean: clean-smart-contracts clean-bindings clean-deployments

fmt:
    forge fmt

check-fmt:
    forge fmt --check

install-deps:
    pnpm install
    forge soldeer install

# compile smart contracts
build-smart-contracts:
    forge build

test:
    forge test

# generate LCOV coverage report
coverage:
    forge coverage --report lcov --lcov-version 2
    genhtml -o coverage lcov.info --rc derive_function_end_line=0

# clean smart contracts artifacts
clean-smart-contracts:
    forge clean

# clean generated bindings
clean-bindings:
    rm -rf {{BINDINGS_DIR}}

# clean deployment artifacts
clean-deployments:
    rm -rf {{DEPLOYMENTS_DIR}} {{DEVNET_STATE}}

# generate Rust bindings from Solidity code
bind: clean-bindings
    forge bind --alloy --select "{{BINDINGS_FILTER}}" \
        --module --bindings-path {{BINDINGS_DIR}} \
        --skip-extra-derives \
        --root {{SRC_DIR}}

build-devnet: \
        (deploy-core \
            "--anvil.preserve-historical-states"
            "--anvil.dump-state" DEVNET_STATE \
            "--write-deployments" DEPLOYMENTS_DIR)

deploy-core *OPTS: \
        (deploy-prt-core OPTS) \
        (deploy "cannonfile.toml" OPTS)

deploy-prt-core *OPTS:
    just -f ../../prt/contracts/justfile deploy-core {{OPTS}}

deploy CANNONFILE *OPTS:
    pnpm cannon build {{CANNONFILE}} {{OPTS}}
