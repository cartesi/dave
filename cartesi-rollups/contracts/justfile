# Variables
BINDINGS_DIR := "./bindings-rs/src/contract"
DEPLOYMENTS_DIR := "./deployments"
SRC_DIR := "."
BINDINGS_FILTER := "DaveConsensus"

DEVNET_PRIVATE_KEY := "0xac0974bec39a17e36ba4a6b4d238ff944bacb478cbed5efcae784d7bf4f2ff80"
DEVNET_RPC_URL := "http://127.0.0.1:8545"

default: build

build: build-smart-contracts bind
clean: clean-smart-contracts clean-bindings clean-deployments

fmt:
    forge fmt

check-fmt:
    forge fmt --check

install-deps:
    pnpm install
    forge soldeer install

# compile smart contracts
build-smart-contracts:
    forge build

test:
    forge test

# generate LCOV coverage report
coverage:
    forge coverage --report lcov --lcov-version 2
    genhtml -o coverage lcov.info --rc derive_function_end_line=0

# clean smart contracts artifacts
clean-smart-contracts:
    forge clean

# clean generated bindings
clean-bindings:
    rm -rf {{BINDINGS_DIR}}

# clean cannon deployments
clean-deployments:
    rm -rf {{DEPLOYMENTS_DIR}}

# generate Rust bindings from Solidity code
bind: clean-bindings
    forge bind --alloy --select {{BINDINGS_FILTER}} \
        --module --bindings-path {{BINDINGS_DIR}} \
        --skip-extra-derives \
        --root {{SRC_DIR}}

deploy-core *OPTS: \
        (deploy-prt-core OPTS) \
        (deploy "cannonfile.toml" OPTS)

deploy-instance INITIAL_HASH *OPTS: \
        (deploy-prt-core OPTS) \
        (deploy-core OPTS) \
        (deploy "cannonfile.prod-instance.toml" ("initialHash=" + INITIAL_HASH) OPTS)

deploy-instance-dev INITIAL_HASH *OPTS: \
        (deploy-prt-core-dev OPTS) \
        (deploy-dev "cannonfile.dev-instance.toml" ("initialHash=" + INITIAL_HASH) OPTS)

deploy-prt-core *OPTS:
    just -f ../../prt/contracts/justfile deploy-core {{OPTS}}

deploy-prt-core-dev *OPTS:
    just -f ../../prt/contracts/justfile deploy-core-dev {{OPTS}}

deploy-dev CANNONFILE *OPTS: \
        (deploy CANNONFILE OPTS \
            "--wipe" \
            "--rpc-url" DEVNET_RPC_URL \
            "--private-key" DEVNET_PRIVATE_KEY)

deploy CANNONFILE *OPTS:
    pnpm cannon build {{CANNONFILE}} {{OPTS}}
