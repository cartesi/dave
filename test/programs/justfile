download-deps: clean-deps
  wget https://github.com/cartesi/image-kernel/releases/download/v0.20.0/linux-6.5.13-ctsi-1-v0.20.0.bin \
    -O ./linux.bin
  wget https://github.com/cartesi/machine-emulator-tools/releases/download/v0.17.1/rootfs-tools.ext2 \
    -O ./rootfs.ext2

clean-deps:
  rm -f rootfs.ext2
  rm -f linux.bin

# all programs
build-programs: build-yield build-echo build-honeypot build-compute
clean-programs: clean-yield clean-echo clean-honeypot
clean-program prog:
  rm -f {{prog}}/_anvil.log {{prog}}/addresses {{prog}}/anvil_state.json
  rm -rf {{prog}}/machine-image

# yield
build-yield: clean-yield
  cartesi-machine --ram-image=./linux.bin \
    --flash-drive=label:root,filename:./rootfs.ext2 \
    --no-rollback --store=./yield/machine-image \
    -- "while true; do yield manual rx-accepted; yield manual rx-rejected; done"
  ./build_anvil_state.sh "yield"
clean-yield: (clean-program "yield")

# echo
build-echo: clean-echo
  cartesi-machine --ram-image=./linux.bin \
    --flash-drive=label:root,filename:./rootfs.ext2 \
    --no-rollback --store=./echo/machine-image \
    -- "ioctl-echo-loop --vouchers=1 --notices=1 --reports=1 --verbose=1 --reject=2"
  ./build_anvil_state.sh "echo"
clean-echo: (clean-program "echo")

# honeypot
build-honeypot: clean-honeypot
  ./build_anvil_state.sh "honeypot"
clean-honeypot:
  rm -f clean-honeypot/_anvil.log clean-honeypot/addresses clean-honeypot/anvil_state.json

# honeypot/honeypot (requires docker)
build-honeypot-snapshot: clean-honeypot-snapshot
  mkdir -p honeypot/machine-image
  curl -L \
    https://github.com/cartesi/honeypot/releases/download/v2.0.0/honeypot-snapshot-localhost.tar.gz | \
    tar -xz -C honeypot/machine-image
clean-honeypot-snapshot: clean-honeypot
  rm -rf honeypot/machine-image

# compute
build-compute: clean-compute
  cartesi-machine --ram-image=./linux.bin \
    --flash-drive=label:root,filename:./rootfs.ext2 \
    --no-rollback --store=./compute/machine-image \
    --max-mcycle=0 \
    -- "while dd if=/dev/zero bs=1M count=64 2>/dev/null | md5sum >/dev/null; do :; done"
clean-compute:
  rm -rf compute/machine-image
