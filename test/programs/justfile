download-deps: clean-deps
  wget https://github.com/cartesi/image-kernel/releases/download/v0.20.0/linux-6.5.13-ctsi-1-v0.20.0.bin \
    -O ./linux.bin
  wget https://github.com/cartesi/machine-emulator-tools/releases/download/v0.16.1/rootfs-tools-v0.16.1.ext2 \
    -O ./rootfs.ext2

clean-deps:
  rm -f rootfs.ext2
  rm -f linux.bin

# all programs
build-programs: build-yield build-echo build-honeypot
clean-programs: clean-yield clean-echo clean-honeypot
clean-program prog:
  rm -f {{prog}}/_anvil.log {{prog}}/addresses {{prog}}/anvil_state.json
  rm -rf {{prog}}/machine-image

# yield
build-yield: clean-yield
  cartesi-machine --ram-image=./linux.bin \
    --flash-drive=label:root,filename:./rootfs.ext2 \
    --no-rollback --store=./yield/machine-image \
    -- "while true; do yield manual rx-accepted; done"
  ./build_anvil_state.sh "yield"
clean-yield: (clean-program "yield")

# echo
build-echo: clean-echo
  cartesi-machine --ram-image=./linux.bin \
    --flash-drive=label:root,filename:./rootfs.ext2 \
    --no-rollback --store=./echo/machine-image \
    -- "ioctl-echo-loop --vouchers=1 --notices=1 --reports=1 --verbose=1"
  ./build_anvil_state.sh "echo"
clean-echo: (clean-program "echo")

# honeypot
build-honeypot: clean-honeypot
  cp -r honeypot/honeypot/snapshot honeypot/machine-image/
  ./build_anvil_state.sh "honeypot"
clean-honeypot: (clean-program "honeypot")

# honeypot/honeypot (requires docker)
build-honeypot-snapshot: clean-honeypot-snapshot
  make -C honeypot/honeypot snapshot
clean-honeypot-snapshot: clean-honeypot
  make -C honeypot/honeypot clean

