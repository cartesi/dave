BINDINGS_DIR := "./bindings-rs/src/contract"
DEPLOYMENTS_DIR := "./deployments"
SRC_DIR := "."
BINDINGS_FILTER := "^[^I].+TournamentFactory|LeafTournament|RootTournament|^Tournament$"

export PRIVATE_KEY := "0xac0974bec39a17e36ba4a6b4d238ff944bacb478cbed5efcae784d7bf4f2ff80"
ANVIL_ENDPOINT := "http://127.0.0.1:8545"
ANVIL_CHAIN_ID := "31337"

default: build

build: build-smart-contracts bind
clean: clean-smart-contracts clean-bindings clean-deployments

build-smart-contracts:
    forge build

test:
    forge test --no-match-contract ".*FfiTest"
    forge test --match-contract ".*FfiTest" --no-match-test ".*Fuzzy" --ffi

test-fuzzy:
    forge test --match-contract ".*FfiTest" --match-test ".*Fuzzy" --ffi

clean-smart-contracts:
    forge clean

fmt:
    forge fmt

check-fmt:
    forge fmt --check

install-deps:
    pnpm install
    forge soldeer install

clean-bindings:
    rm -rf {{BINDINGS_DIR}}

clean-deployments:
    rm -rf {{DEPLOYMENTS_DIR}}

bind: clean-bindings
    forge bind --alloy --select "{{BINDINGS_FILTER}}" \
    --module --bindings-path {{BINDINGS_DIR}} \
    --root {{SRC_DIR}}

deploy-dev INITIAL_HASH: deploy-core (deploy-instance INITIAL_HASH)

deploy-core: (deploy "cannonfile.toml")

deploy-instance INITIAL_HASH: deploy-core (deploy "cannonfile.instance.toml" ("initialHash=" + INITIAL_HASH))

deploy CANNONFILE *SETTINGS:
    pnpm cannon build \
        --wipe \
        --rpc-url {{ANVIL_ENDPOINT}} \
        --private-key {{PRIVATE_KEY}} \
        --write-deployments {{DEPLOYMENTS_DIR}} \
        {{CANNONFILE}} \
        {{SETTINGS}}
