BINDINGS_DIR := "./bindings-rs/src/contract"
DEPLOYMENTS_DIR := "./deployments"
SRC_DIR := "."
BINDINGS_FILTER := "^[^I].+TournamentFactory|LeafTournament|RootTournament|^Tournament$"

DEVNET_PRIVATE_KEY := "0xac0974bec39a17e36ba4a6b4d238ff944bacb478cbed5efcae784d7bf4f2ff80"
DEVNET_RPC_URL := "http://127.0.0.1:8545"

default: build

build: build-smart-contracts bind
clean: clean-smart-contracts clean-bindings clean-deployments

build-smart-contracts:
    forge build

test-all: test-disputes test-stf test-stf-fuzzy

test-disputes:
    forge test --no-match-contract ".*FfiTest"

test-stf:
    forge test --match-contract ".*FfiTest" --no-match-test ".*Fuzzy" --ffi

test-stf-fuzzy:
    forge test --match-contract ".*FfiTest" --match-test ".*Fuzzy" --ffi

clean-smart-contracts:
    forge clean

fmt:
    forge fmt

check-fmt:
    forge fmt --check

install-deps:
    pnpm install
    forge soldeer install

clean-bindings:
    rm -rf {{BINDINGS_DIR}}

clean-deployments:
    rm -rf {{DEPLOYMENTS_DIR}}

bind: clean-bindings
    forge bind --alloy --select "{{BINDINGS_FILTER}}" \
    --module --bindings-path {{BINDINGS_DIR}} \
    --skip-extra-derives \
    --root {{SRC_DIR}}

deploy-core *OPTS: \
        (deploy "cannonfile.toml" OPTS)

deploy-core-dev *OPTS: \
        (deploy-dev "cannonfile.toml" OPTS)

deploy-instance-dev INITIAL_HASH *OPTS: \
        (deploy-core-dev OPTS) \
        (deploy-dev "cannonfile.dev-instance.toml" ("initialHash=" + INITIAL_HASH) OPTS)

deploy-dev CANNONFILE *OPTS: \
        (deploy CANNONFILE OPTS \
            "--wipe" \
            "--rpc-url" DEVNET_RPC_URL \
            "--private-key" DEVNET_PRIVATE_KEY)

deploy CANNONFILE *OPTS:
    pnpm cannon build {{CANNONFILE}} {{OPTS}}
