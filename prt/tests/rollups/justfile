ECHO_DIR := "../../../test/programs/echo"
HONEYPOT_DIR := "../../../test/programs/honeypot"
YIELD_DIR := "../../../test/programs/yield"
ANVIL_PATH := "../../../cartesi-rollups/contracts/state.json"
DEPLOYMENTS_DIR := "../../../cartesi-rollups/contracts/deployments/31337"

WEB3_PRIVATE_KEY := "0xac0974bec39a17e36ba4a6b4d238ff944bacb478cbed5efcae784d7bf4f2ff80"

# run PRT rollups test
test MACH_PATH SCRIPT:
  rm -rf _state
  WEB3_PRIVATE_KEY={{WEB3_PRIVATE_KEY}} \
  ANVIL_PATH=`realpath {{ANVIL_PATH}}` \
  TEMPLATE_MACHINE=`realpath {{MACH_PATH}}/machine-image` \
  TEMPLATE_MACHINE_HASH=0x`xxd -p -c32 {{MACH_PATH}}/machine-image/hash` \
  DAVE_APP_FACTORY=`jq -r .address {{DEPLOYMENTS_DIR}}/DaveAppFactory.json` \
  INPUT_BOX=`jq -r .address {{DEPLOYMENTS_DIR}}/InputBox.json` \
  ERC20_PORTAL=`jq -r .address {{DEPLOYMENTS_DIR}}/ERC20Portal.json` \
  ERC20_TOKEN=`jq -r .address {{DEPLOYMENTS_DIR}}/TestFungibleToken.json` \
  lua5.4 test_cases/{{SCRIPT}}.lua

# run PRT rollups echo test
test-echo:
  just test {{ECHO_DIR}} simple

# run PRT rollups honeypot test
test-honeypot-all: \
        (test-honeypot-case "honeypot_deposit_withdrawal") \
        (test-honeypot-case "simple_no_input") \
        (test-honeypot-case "stf_all") \
        (test-honeypot-case "big_input") \
        (test-honeypot-case "gc_match") \
        (test-honeypot-case "gc_tournament") \
        (test-honeypot-case "bad_commitment")

# run PRT rollups yield test
test-yield-all: \
        (test-yield-case "simple_no_input") \
        (test-yield-case "stf_all") \
        (test-yield-case "big_input") \
        (test-yield-case "gc_match") \
        (test-yield-case "gc_tournament") \
        (test-yield-case "bad_commitment")

test-honeypot-ci: \
        (test-honeypot-case "simple")

test-yield-ci: \
        (test-yield-case "simple")

test-honeypot-case CASE:
  just test {{HONEYPOT_DIR}} {{CASE}}

test-yield-case CASE:
  just test {{YIELD_DIR}} {{CASE}}

# read logs from PRT Rollups node, run in separate terminal after `test-echo`
read-node-logs:
  cat dave.log; tail -f dave.log

# sepolia-honeypot
download-sepolia-honeypot: clean-sepolia-honeypot-snapshot
  mkdir -p sepolia/machine-image
  curl -L \
    https://github.com/cartesi/honeypot/releases/download/v3.0.0/honeypot-snapshot-sepolia.tar.gz | \
    tar -xz -C sepolia/machine-image
clean-sepolia-honeypot-snapshot:
  rm -rf sepolia/machine-image

run-sepolia SCRIPT:
  ( set -a; source sepolia/.env; exec lua sepolia/{{SCRIPT}}.lua)
