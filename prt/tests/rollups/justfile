ECHO_DIR := "../../../test/programs/echo"
HONEYPOT_DIR := "../../../test/programs/honeypot"

WEB3_PRIVATE_KEY := "0xac0974bec39a17e36ba4a6b4d238ff944bacb478cbed5efcae784d7bf4f2ff80"

# run PRT rollups test
test MACH_PATH SCRIPT:
  rm -rf _state
  WEB3_PRIVATE_KEY={{WEB3_PRIVATE_KEY}} \
  ANVIL_PATH=`realpath {{MACH_PATH}}/anvil_state.json` \
  TEMPLATE_MACHINE=`realpath {{MACH_PATH}}/machine-image` \
  APP=`sed '3q;d' {{MACH_PATH}}/addresses` \
  CONSENSUS=`sed '2q;d' {{MACH_PATH}}/addresses` \
  INPUT_BOX=`sed '1q;d' {{MACH_PATH}}/addresses` \
  lua test_cases/{{SCRIPT}}.lua

# run PRT rollups echo test
test-echo:
  just test {{ECHO_DIR}} simple

# run PRT rollups honeypot test
test-honeypot-all:
  just test {{HONEYPOT_DIR}} stf_all
  just test {{HONEYPOT_DIR}} big_input

test-honeypot-ci:
  just test {{HONEYPOT_DIR}} simple

# read logs from PRT Rollups node, run in separate terminal after `test-echo`
read-node-logs:
  cat dave.log; tail -f dave.log
