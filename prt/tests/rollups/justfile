ANVIL_LOAD_PATH := "../../../cartesi-rollups/contracts/state.json"
DEPLOYMENTS_DIR := "../../../cartesi-rollups/contracts/deployments/31337"

WEB3_PRIVATE_KEY := "0xac0974bec39a17e36ba4a6b4d238ff944bacb478cbed5efcae784d7bf4f2ff80"

# run PRT rollups test
test PROGRAM SCRIPT:
  rm -rf _state
  WEB3_PRIVATE_KEY={{WEB3_PRIVATE_KEY}} \
  ANVIL_LOAD_PATH=`realpath {{ANVIL_LOAD_PATH}}` \
  ANVIL_DUMP_PATH="anvil_{{PROGRAM}}_{{SCRIPT}}.json" \
  TEMPLATE_MACHINE=`realpath ../../../test/programs/{{PROGRAM}}/machine-image` \
  TEMPLATE_MACHINE_HASH=0x`xxd -p -c32 ../../../test/programs/{{PROGRAM}}/machine-image/hash` \
  DAVE_APP_FACTORY=`jq -r .address {{DEPLOYMENTS_DIR}}/DaveAppFactory.json` \
  INPUT_BOX=`jq -r .address {{DEPLOYMENTS_DIR}}/InputBox.json` \
  ERC20_PORTAL=`jq -r .address {{DEPLOYMENTS_DIR}}/ERC20Portal.json` \
  ERC20_TOKEN=`jq -r .address {{DEPLOYMENTS_DIR}}/TestFungibleToken.json` \
  lua5.4 test_cases/{{SCRIPT}}.lua

# run PRT rollups echo test
test-echo: \
        (test "echo" "simple")

# run PRT rollups honeypot test
test-honeypot-all: \
        (test "honeypot" "deposit_withdrawal") \
        (test "honeypot" "simple_no_input") \
        (test "honeypot" "stf_all") \
        (test "honeypot" "big_input") \
        (test "honeypot" "gc_match") \
        (test "honeypot" "gc_tournament") \
        (test "honeypot" "bad_commitment")

# run PRT rollups yield test
test-yield-all: \
        (test "yield" "simple_no_input") \
        (test "yield" "stf_all") \
        (test "yield" "big_input") \
        (test "yield" "gc_match") \
        (test "yield" "gc_tournament") \
        (test "yield" "bad_commitment")

test-honeypot-ci: \
        (test "honeypot" "simple")

test-yield-ci: \
        (test "yield" "simple")

test-honeypot-case CASE: \
        (test "honeypot" CASE)

test-yield-case CASE: \
        (test "yield" CASE)

# read logs from PRT Rollups node, run in separate terminal after `test-echo`
read-node-logs:
  cat dave.log; tail -f dave.log

# sepolia-honeypot
download-sepolia-honeypot: clean-sepolia-honeypot-snapshot
  mkdir -p sepolia/machine-image
  curl -L \
    https://github.com/cartesi/honeypot/releases/download/v3.0.0/honeypot-snapshot-sepolia.tar.gz | \
    tar -xz -C sepolia/machine-image
clean-sepolia-honeypot-snapshot:
  rm -rf sepolia/machine-image

run-sepolia SCRIPT:
  ( set -a; source sepolia/.env; exec lua sepolia/{{SCRIPT}}.lua)
